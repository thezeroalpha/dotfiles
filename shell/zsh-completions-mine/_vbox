#compdef vbox
_vboxmachines() {
    VBoxManage list vms | egrep -o '^"[^"]+"' 2>/dev/null | sed -e 's|"||g' | while read machine; do
        _wanted 'machine' expl 'machine' compadd $machine
    done
}
_vbox() {
  local -a arguments
  arguments=(
    'start:start a VM'
    'stop:stop a VM'
    'open:open a VM, starting it if necessary'
    'pause:pause a VM'
    'suspend:suspend a VM'
    'resume:resume a paused VM'
    'ls:list known VMs'
    'info:get information about a VM'
    'status:get status of a VM'
    'running:list currently running VMs'
    'share:share a local folder'
    'unshare:unshare a local folder'
    'sharetmp:temporarily share a local folder'
    'shared:list shared folders for a VM'
  )
  local context state line expl
  local -A opt_args
  _arguments '*:: :->subcmds' && return 0

  if (( CURRENT == 1 )); then
    _describe -t commands "vbox commands" arguments -V1
    return
  fi

  case "$words[1]" in
    share|sharetmp)
      _arguments \
        ':hostpath:_files -/' \
        :machine:_vboxmachines \
        ':name: :'
      ;;
    unshare)
      _arguments \
        ':hostpath:_files -/' \
        :machine:_vboxmachines
      ;;
    start|stop|open|status|info|shared|pause|suspend|resume)
      _arguments \
        :machine:_vboxmachines
      ;;
  esac
  return 1
}
