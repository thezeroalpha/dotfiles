" vim: foldmethod=marker foldlevel=0
let mapleader=" "   " Set the mapleader to be space

" Plugins {{{1
" Installation {{{2
" Install vim-plug if needed
if empty(glob('~/.vim/autoload/plug.vim'))
  silent execute "!curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim"
  autocmd VimEnter * PlugInstall | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')
" Vim tester
Plug 'junegunn/vader.vim'

" A color table with xterm color codes
Plug 'guns/xterm-color-table.vim'

" Simple commenting
Plug 'tpope/vim-commentary'

" Emmet but simpler
Plug 'rstacruz/sparkup'

" Endwise - smart do-end, if-fi, if-end, case-esac, etc.
Plug 'tpope/vim-endwise'

" Surround - super useful plugin for surrounding stuff with quotes/brackets/tags
Plug 'tpope/vim-surround'

" Eunuch - shell commands but in vim
Plug 'tpope/vim-eunuch'

" Project management
Plug 'tpope/vim-projectionist'

" A tag overview on the right side
Plug 'majutsushi/tagbar'

" Personal hypertext files
Plug 'vimwiki/vimwiki'

" Git wrapper from tpope
Plug 'tpope/vim-fugitive'

" Undo tree visualiser
Plug 'simnalamburt/vim-mundo'

" Quickfix window mappings
Plug 'romainl/vim-qf'

" Repeat everything with '.'
Plug 'tpope/vim-repeat'

" Distraction-free editing
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'

" Better CSV editing
Plug 'chrisbra/csv.vim'

" Display ANSI color codes
Plug 'vim-scripts/AnsiEsc.vim'

" Disable hlsearch after finished searching
Plug 'romainl/vim-cool'

" Vim + latex
Plug 'lervag/vimtex'

" Snippets
Plug 'SirVer/ultisnips'

" Improve K
Plug 'gastonsimone/vim-dokumentary'

" Fuzzy finder in vim
Plug 'junegunn/fzf', { 'dir': '$HOME/.dotfiles/tools/fzf', 'do': './install --all --xdg --no-fish'}
Plug 'junegunn/fzf.vim'

" Better abbrevation and substitution
Plug 'tpope/vim-abolish'

" Highlight hex colors
Plug 'chrisbra/colorizer'

" Pywal scheme in vim
Plug 'dylanaraps/wal.vim'

" Show git changes in the sign column
Plug 'airblade/vim-gitgutter'

" Bracket bindings for filetypes
Plug 'arp242/jumpy.vim'

" Substitute preview
Plug 'markonm/traces.vim'

" Omnifuncs
Plug 'myhere/vim-nodejs-complete'
Plug 'othree/html5.vim'
Plug 'vim-scripts/OmniCppComplete'
Plug 'vim-scripts/pythoncomplete'

" Alignment
Plug 'junegunn/vim-easy-align'
"
" Peek contents of registers
Plug 'junegunn/vim-peekaboo'

" Latex symbol to unicode
Plug 'joom/latex-unicoder.vim'

" Smooth ^U/^D scrolling
Plug 'psliwka/vim-smoothie'

" Asynchronous popery
Plug 'tpope/vim-dispatch'

" Vimscript testing
Plug 'tpope/vim-scriptease'

" Set 'path', 'suffixesadd', 'include', 'includeexpr', 'define'
"   automatically for selected filetypes.
Plug 'tpope/vim-apathy'

" Schemer - colorscheme creator
Plug 'thezeroalpha/vim-schemer'

" Make ins-completion relative to current file
Plug 'thezeroalpha/vim-relatively-complete'

" Run visually selected lines as Vim commands
Plug 'thezeroalpha/vim-visualrun'

" Select files with LF
Plug 'thezeroalpha/vim-lf'

" Literate programming in Markdown
Plug 'thezeroalpha/vim-literate-markdown'

" Lightweight REPL
Plug 'axvr/zepl.vim'

" Center the window vertically (like Goyo but keeps statusline/whatever)
Plug 'jmckiern/vim-venter'

" Edit files directly in quickfix window
Plug 'stefandtw/quickfix-reflector.vim'

" Browse github repositories
Plug 'skanehira/gh.vim'

" Minesweeper
Plug 'iqxd/vim-mine-sweeping'

" Easier text object definition
Plug 'kana/vim-textobj-user'
let installed_textobjs = {
      \ 'deathlyfrantic/vim-textobj-blanklines': 'a<Space>/i<Space> for groups of blank (only whitespace) lines',
      \ 'kana/vim-textobj-entire': 'ae/ie for the entire region of the current buffer',
      \ 'somini/vim-textobj-fold': 'az/iz for a block of folded lines; iz does not include fold marker lines',
      \ 'kana/vim-textobj-function': 'af/if and aF/iF for a function / extensible for any language',
      \ 'kana/vim-textobj-lastpat': 'a//i/ for a region matched to the last search pattern',
      \ 'kana/vim-textobj-line': 'al/il for the current line' }
for reponame in keys(installed_textobjs) | Plug reponame | endfor

Plug 'dense-analysis/ale'

call plug#end()
" }}}2
" Config {{{2
" Tagbar {{{3
" Maps
nnoremap <leader>tt :TagbarToggle<CR>
nnoremap <leader>to :TagbarOpen fj<CR>

" tagbar language definitions
let g:tagbar_type_vimwiki = {
      \   'ctagstype':'vimwiki'
      \ , 'kinds':['h:header']
      \ , 'sro':'&&&'
      \ , 'kind2scope':{'h':'header'}
      \ , 'sort':0
      \ , 'ctagsbin':'vwtags.py'
      \ , 'ctagsargs': 'default'
      \ }

let g:tagbar_type_go = {
      \ 'ctagstype' : 'go',
      \ 'kinds'     : [
        \ 'p:package',
        \ 'i:imports:1',
        \ 'c:constants',
        \ 'v:variables',
        \ 't:types',
        \ 'n:interfaces',
        \ 'w:fields',
        \ 'e:embedded',
        \ 'm:methods',
        \ 'r:constructor',
        \ 'f:functions'
        \ ],
        \ 'sro' : '.',
        \ 'kind2scope' : {
          \ 't' : 'ctype',
          \ 'n' : 'ntype'
          \ },
          \ 'scope2kind' : {
          \ 'ctype' : 't',
          \ 'ntype' : 'n'
          \ },
          \ 'ctagsbin'  : 'gotags',
          \ 'ctagsargs' : '-sort -silent'
          \ }
let g:tagbar_type_scss = {
      \ 'ctagstype' : 'scss',
      \ 'kinds' : [
        \ 'm:Mixins',
        \ 'f:Functions',
        \ 'v:Variables',
        \ 'c:Classes',
        \ 'i:IDs',
        \ 't:Tags',
        \ 'd:Media',
        \ 'p:Placeholders'
        \ ],
        \ 'sort': 0
        \ }
let g:tagbar_type_conf = {
      \ 'ctagstype' : 'lfrc',
      \ 'kinds' : [
        \ 'c:Commands',
        \ 'm:Maps'
        \ ],
        \ 'sort': 0
        \ }
let g:tagbar_type_zsh = {
      \ 'ctagstype' : 'zsh',
      \ 'kinds' : [
        \ 's:Source scripts',
        \ 'e:Environment variables',
        \ 'f:Functions',
        \ 'm:Mappings'
        \ ],
        \ 'sort': 0
        \ }
" Doge {{{3
let g:doge_mapping = '<leader>gd'
" Goyo {{{3
let g:goyo_width = "80%"
let g:goyo_height = "85%"
" Vimtex {{{3
let g:tex_flavor='latex'
let g:vimtex_view_method='general'
let g:vimtex_quickfix_mode=0
let g:tex_conceal='abdmg'
let g:vimtex_compiler_latexmk = {
      \ 'build_dir' : 'build',
      \ 'callback' : 1,
      \ 'continuous' : 1,
      \ 'executable' : 'latexmk',
      \ 'hooks' : [],
      \ 'options' : [
        \   '-verbose',
        \   '-file-line-error',
        \   '-synctex=1',
        \   '-interaction=nonstopmode',
        \ ],
        \}
" Ultisnips {{{3
let g:UltiSnipsExpandTrigger = '<tab>'
let g:UltiSnipsJumpForwardTrigger = '<tab>'
let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'
let g:UltiSnipsSnippetDirectories = ['~/.dotfiles/vim/ultisnips']
let g:UltiSnipsEditSplit = "vertical"
cabbrev USE UltiSnipsEdit
" Complete ultisnips with <C-x><C-u>, which is coincidentally the same mapping as user completion
" (custom function btw)
set completefunc=ultisnips_complete#ListSnippets
" Dokumentary {{{3
" Improve what K does
let g:dokumentary_docprgs = {'ruby': 'ri {0} | col -b', 'c': ':Man {0}'}
" Abolish {{{3
" Work with variants of words easily
let g:abolish_save_file = '"$HOME"/.dotfiles/vim/abolish_save_file.vim'
" Fzf {{{3
set rtp+="$HOME"/.dotfiles/tools/fzf
function! s:build_quickfix_list(lines)
  call setqflist(map(copy(a:lines), '{ "filename": v:val }'))
  copen
  cc
endfunction
let g:fzf_action = {
      \ 'ctrl-f': function('s:build_quickfix_list'),
      \ 'ctrl-t': 'tabedit',
      \ 'ctrl-p': 'split',
      \ 'ctrl-v': 'vsplit' }
let g:fzf_layout = {'window': { 'width': 0.9, 'height': 0.4 } }
let g:fzf_preview_window = 'right:60%'
" Jump to existing buffer/window if possible
let g:fzf_buffers_jump = 1

nnoremap <leader>F :Files<CR>
nnoremap <leader><c-f> :Files<space>
nnoremap <leader>B :Buffers<CR>
nnoremap <leader>G :Lines<CR>
nnoremap <leader>W :BLines<CR>
nnoremap <leader>? :Helptags<CR>
nnoremap <leader>T :Tags<CR>
nnoremap <leader>/ :Ag<CR>
nnoremap <leader>M :Marks<CR>
nnoremap <leader>; :History:<CR>

" Search for Todos/Fixmes
command! Todo Ag ((TO ?DO)|FIXME):?<space>?
nnoremap <leader>! :Todo<CR>

" Set a filetype
cabbrev setf Filetypes

command! Scripts call fzf#run(fzf#wrap({'source': 'command find "$(readlink ~/.scripts)" -type f -or -type l -not -ipath "*/.git/*"', 'options': '--multi --reverse --inline-info --prompt="scripts> "', 'sink': 'sp'}))
command! Configs call fzf#run(fzf#wrap({'source': 'command find ~/.dotfiles -type f ! -ipath "*/.git/*" ! -ipath "*/bin/*" ! -ipath "*/oh-my-zsh/*" ! -name ".DS_Store"', 'options': '--multi --reverse --inline-info --prompt="configs> "', 'sink': 'sp'}))
" Vimwiki {{{3
let tlist_vimwiki_settings = 'wiki;h:Headers'
let g:vimwiki_table_mappings = 0   " avoid vimwiki conflict with Ultisnips

let wiki = {}
let wiki.path = '/Users/alex/Documents/Syncthing/vimwiki/'
let wiki.path_html = '/Users/alex/Documents/Syncthing/vimwiki_html'
let wiki.template_path = '/Users/alex/Documents/Syncthing/vimwiki/templates'
let wiki.template_ext = '.tpl'
let wiki.syntax = 'default'
let wiki.ext = '.txt'
let g:vimwiki_list = [wiki]
let g:vimwiki_global_ext = 1
let g:vimwiki_ext2syntax = {'.wiki': 'default'}

nnoremap <leader>wc :VimwikiCheckLinks<CR>
nmap <leader>ww <Plug>VimwikiIndex
" Sessions {{{3
nmap <leader>ss <Plug>SaveSession
nmap <leader>sl <Plug>LoadSession
nmap <leader>sd <Plug>DeleteSession
nmap <leader>sq <Plug>CloseSession
" Sparkup {{{3
let g:sparkupExecuteMapping = '<C-x><C-e><CR>'
let g:sparkupNextMapping = '<C-x><C-e><tab>'
" Haskell {{{3
let hs_highlight_delimiters = 1
let hs_highlight_boolean = 1
let hs_highlight_types = 1
let hs_highlight_more_types = 1
" EasyAlign {{{3
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <Plug>(LiveEasyAlign)

" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(LiveEasyAlign)

" Keep :ascii as a map
nnoremap <leader>ga :<c-u>ascii<cr>
" Peekaboo {{{3
let g:peekaboo_compact=1
" Mundo {{{3
nnoremap <leader>u :MundoToggle<CR>
" Vim-qf {{{3
" quickfix
nmap <leader>qn <Plug>(qf_qf_next)
nmap <leader>qp <Plug>(qf_qf_previous)
nmap <leader>qf <Plug>(qf_qf_toggle)

" location
nmap <leader>ln <Plug>(qf_loc_next)
nmap <leader>lp <Plug>(qf_loc_previous)
nmap <leader>ll <Plug>(qf_loc_toggle)
nnoremap <leader>lb :lbefore<CR>
nnoremap <leader>la :lafter<CR>
nnoremap <leader>lf :lfirst<CR>

" Disable auto-open because it interferes with Dispatch reverting makeprg
" See here https://github.com/tpope/vim-dispatch/issues/254
let g:qf_auto_open_quickfix = 0
" Dispatch {{{3
" Disable default maps
let g:dispatch_no_maps = 1

" Only enable those I'll actually use
nnoremap `<CR> :Dispatch<CR>
nnoremap '<CR> :Start<CR>
" netrw {{{3
let g:netrw_banner = 0                              " hide the banner
let g:netrw_fastbrowse = 2                          " only get dir list if not seen, or manually refreshed
let g:netrw_liststyle = 0                           " thin listing
let g:netrw_winsize = 25                            " open at 25% size
let g:netrw_keepdir = 0                             " keep the current directory the same as the browsing directory.
let g:netrw_retmap = 0                              " double-click to return to browsing
let g:netrw_list_hide = &wildignore                 " hide everything in &wildignore
let g:netrw_list_hide .= ',\(^\|\s\s\)\zs\.\S\+'    " as well as dotfiles
let g:netrw_special_syntax = 1                      " use special syntax groups for certain files in browser
" latex-unicoder {{{3
let g:unicoder_no_map = 1
inoremap <C-l> <Esc>:call unicoder#start(1)<CR>
" colorizer {{{3
let g:colorizer_colornames = 0
command! CH ColorHighlight
command! CC ColorClear
" Schemer {{{3
" List color group
nmap <C-P> <Plug>SchemerSynstack
nmap <leader><C-E><C-L> <Plug>SchemerEdit
" Relatively complete {{{3
" Replace default file completion with 'smart' relative complete
imap <C-x><C-f> <Plug>RelativelyCompleteFile
" Visualrun {{{3
" Run selected commands (silent)
vmap <silent> <leader><cr> <Plug>VisualRunCommands
" Zepl {{{3
runtime! zepl/contrib/control_characters.vim
      \ zepl/contrib/load_files.vim
      \ zepl/contrib/python.vim
let g:repl_config = {
      \ 'python': {
        \ 'cmd': 'python3',
        \ 'formatter': function('zepl#contrib#python#formatter'),
        \ 'load_file': 'from %s import *'
      \ },
      \ 'javascript': { 'cmd': 'node' },
      \ 'ruby': { 'cmd': 'irb', 'load_file': 'load "%s"' },
      \ 'r': { 'cmd': 'R' }
\ }
nnoremap gzr :Repl<CR>
" Medieval {{{3
let g:medieval_langs = ['python=python3', 'ruby', 'sh', 'bash', 'haskell=ghci', 'javascript=node']
" LF {{{3
nmap <leader>\ <Plug>LfEdit
nmap <leader><c-\> <Plug>LfSplit
" Ada {{{3
let g:ada_standard_types = 1
let g:ada_space_errors = 1
let g:ada_begin_preproc = 1
let g:ada_omni_with_keywords = 1
" gh.vim {{{3
let g:gh_token = getenv("GITHUB_AUTH_TOKEN")
" ALE {{{3
let g:airline#extensions#ale#enabled = 0
let g:ale_enabled = 0
let g:ale_c_cc_options = '-std=gnu99 -Wall'
let g:ale_c_clangd_options = '-std=gnu99 -Wall'
let g:ale_c_clangformat_options = '-std=gnu99 -Wall'
let g:ale_c_clangtidy_options = '-std=gnu99 -Wall'
let g:ale_c_clangtidy_extra_options = '-std=gnu99 -Wall'
" textobj-user {{{3
call textobj#user#plugin('asterisks', {
\      '-': {
\        '*sfile*': expand('<sfile>:p'),
\        'select-a': 'a*',  '*select-a-function*': 'SelectAAsterisks',
\        'select-i': 'i*',  '*select-i-function*': 'SelectIAsterisks'
\      }
\    })

function! SelectAAsterisks()
  normal! F*
  let end_pos = getpos('.')
  normal! f*
  let start_pos = getpos('.')
  return ['v', start_pos, end_pos]
endfunction

function! SelectIAsterisks()
  normal! T*
  let end_pos = getpos('.')
  normal! t*
  let start_pos = getpos('.')
  return ['v', start_pos, end_pos]
endfunction

" }}}1
" General {{{1
" Put viminfo in ~/.cache
set viminfofile=~/.cache/vim/viminfo

" Set the colors
" Use true color
" Set Vim-specific sequences for RGB colors
let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"

if has('termguicolors')
  set termguicolors
endif

if filereadable($HOME."/.config/dark-theme")
  colorscheme junipero
else
  colorscheme jokull
endif

" where to find tags files
set tags=./tags;~,tags,~/.tags
if has("path_extra")
  " If compiled with path_extra, search for .git/tags upwards in the tree
  "   and stop searching at $HOME
  let &tags = &tags.',.git/tags;~'
else
  let &tags = &tags.',.git/tags'
endif

" follow the 'ignorecase' option when looking for tags
set tagcase=followic

" dictionary files, in order (custom words should appear at the top)
" remember, to generate a wordlist from spellcheck, do :spelldump
set dictionary=~/.vim/words/dict/global/custom.txt
set dictionary+=~/.vim/words/dict/global/3of6all.txt
set dictionary+=/usr/share/dict/words

" thesaurus file
set thesaurus=~/.vim/words/thesaurus/mthesaur.txt

" matchit.vim is default, why not enable it
runtime macros/matchit.vim

" same with :Man
" I'm lazy-loading this because it really affects my startup time.
" I'm not the only one: https://www.reddit.com/r/vim/comments/emx856/lazy_loading_man_page_plugin/
silent! command! -bar -nargs=* Man
      \ delcommand Man |
      \ runtime ftplugin/man.vim |
      \ Man <args>

" For editing binaries
set binary

" Save swapfiles to ~/.local/share/vim/swap
if !isdirectory($HOME.'/.local/share/vim/swap')
  silent call mkdir($HOME.'/.local/share/vim/swap', 'p')
endif
set directory=~/.local/share/vim/swap//
set swapfile

" Save backups to ~/.local/share/vim/backup
if !isdirectory($HOME.'/.local/share/vim/backup')
  silent call mkdir($HOME.'/.local/share/vim/backup', 'p')
endif
set backupdir=~/.local/share/vim/backup//
set nobackup writebackup

" make regexes consistent with other programs (extended)
set magic

" global setting for directories to search for files (e.g. for gf)
" . == directory of current file, ,, == current working directory
set path=.,"$HOME"/.dotfiles/vim/**,,

" global suffixes to add when looking for files (configured otherwise on per-language basis)
set suffixesadd=.vim

" include, includeexpr, define: very language-specific, so no default global value

" global patterns to ignore in wildmenu
set wildignore=*.swp
set wildignorecase

" Persistent undos (useful for plugins too){{{
if has('persistent_undo')
  let myUndoDir = $HOME . '/.local/share/vim/undo'
  silent! call mkdir(myUndoDir, 'p')
  let &undodir = myUndoDir
  set undofile
endif
" }}}

" Hide buffers instead of closing
set hidden

" Dont redraw while executing macros
set lazyredraw

" Encoding & formats
set encoding=utf-8 nobomb
set fileformats=unix,dos,mac

" Number of lines to use for command line
" (this fixes the 'press  to continue' thing on :help)
set cmdheight=1

" Send more characters at a given time
set ttyfast

" Show partial command on last line
set showcmd

" Don't show current mode (it's in the statusline)
set noshowmode
" Command completion
set wildmenu

" Allow the mouse, good for e.g. resizing splits
set mouse=a
" fix issue with not being able to drag resize split windows in tmux
if &term =~ '^screen' || $TMUX !=# ""
  set ttymouse=xterm2
endif

" How to split new windows
set splitbelow splitright

" Allow per-file settings
set modeline
set modelines=5     "within the first/last 5 lines

" Allow italics
set t_ZH=[3m
set t_ZR=[23m

" Since belloff isn't always an option
if exists("&belloff")
  set belloff=showmatch,esc,shell,wildmode,backspace   " Disable beeping if no match is found
endif

" Command-line autocompletion
set wildmode=longest:list,full

" Add everything to sessions
set sessionoptions=buffers,curdir,folds,globals,localoptions,options,resize,tabpages

" Use ag for grepping
set grepprg=ag\ --vimgrep
set updatetime=100
" }}}1
" Editor {{{1
" In general, don't conceal anything
if has('conceal')
  set conceallevel=0
endif

" Change cursor shape between insert and normal mode in iTerm2.app and
" Alacritty
if exists('$TMUX')
  let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
  let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
else
  let &t_SI = "\<Esc>]50;CursorShape=1\x7" " Vertical bar in insert mode
  let &t_EI = "\<Esc>]50;CursorShape=0\x7" " Block in normal mode
endif

" Highlight current line
set cursorline

" Reduce waiting on keys (like esc)
set timeoutlen=1000 ttimeoutlen=50

" Wrap settings - no wrap by default
set nowrap
set textwidth=0
set wrapmargin=0
set linebreak " break at &breakat
" Continue wrapped line on same indent as previous
if has('breakindent')
  set breakindent
  set breakindentopt+=shift:3
endif

" Default formatoptions:
" - q: allow gq
" - n: recognise numbered lists
" - l: don't break long lines in insert mode
" - j: remove comment leader when joining
set formatoptions=qnlj

" Numbered lines
set number
set numberwidth=3
set relativenumber

" with a smart tab
set smarttab

" and 1 tab == 2 spaces
set tabstop=8       " visual length of \t in spaces, should be left at 8
set expandtab       " use spaces instead of tabs
set softtabstop=4   " 4 spaces inserted when TAB pressed
set shiftwidth=2    " 2 spaces when >>
set shiftround      " always shift by multiple of shiftwidth

" Auto indent when starting new line
set autoindent
set copyindent      " copy structure of other indents whn autoindenting

" Enable indenting based on filetype
filetype plugin indent on

" Syntax highlighting
syntax on

" Enable fenced code highlighting in markdown (tpope's plugin, ships with vim)
let g:markdown_fenced_languages = ['html', 'python', 'ruby', 'bash=sh', 'map', 'vim', 'c', 'cpp', 'make']
" Enable markdown folding
let g:markdown_folding = 1


" Folding on indentation
set foldmethod=indent
set foldlevelstart=5      " start with up to 5 levels open
set foldnestmax=10        " unless callback-hell JS

" Show matching brackets
set showmatch
set matchtime=2

" Searching
set hlsearch    " highlight matches
set incsearch   " search while typing
set ignorecase  " ignore case generally
set smartcase   " but not if searching for capital

" Backspace tweaks
set backspace=indent,eol,start
set whichwrap+=<,>,h,l

" Complete from current buffer, other windows, buffers, unloaded buffers, tags,
"   current and included files, and dictionary if spell is set
set complete=.,w,b,u,t,i,kspell
" Use popup menu, also when there is only one match, don't insert until the user selects
"   a match, don't select anything automatically.
set completeopt=menu,menuone,preview

nnoremap <silent><expr> <leader># ":set ".(&relativenumber?'norelativenumber number':'relativenumber number')."<CR>"

set omnifunc=syntaxcomplete#Complete

" Keep cursor off top and bottom of screen
set scrolloff=5

" Which formats to use for incrementing/decrementing
set nrformats=hex,bin

" We write posix shell scripts
let g:is_posix = 1

" Show where lines were wrapped
set showbreak=â‰»
" }}}
" Commands {{{1
command! NoteP vimgrep /NOTE\C/ **/*.* | copen
command! Note vimgrep /NOTE\C/ % | copen
command! FixP vimgrep /FIXME\C/ **/*.* | copen
command! Fix vimgrep /FIXME\C/ % | copen
command! ListFileTypes echo glob($VIMRUNTIME . '/syntax/*.vim')
command! CD cd %:p:h
command! LCD lcd %:p:h
command! TCD tcd %:p:h
command! Maketab set noet ts=2 | %retab!
command! Diff w !diff % -
command! Diffg w !git diff % -
command! Fuckwindows %s///g
command! Hexedit %!xxd
command! Unhex %!xxd -r
command! JsonSimplifyObject %s/^\(\s\{10}\){\n\s\{12\}\(.*\)\n\s\{10\}}\(,\?\)/\1{ \2 }\3
command! BeautifyJson %!python -m json.tool
command! Dos2unix %!dos2unix
command! -nargs=1 Cheat terminal curl cheat.sh/<args>
command! Reveal exe "silent !open ".shellescape(expand("%:p:h")) | redraw!
command! Softwrap setlocal wrap formatoptions-=cat
command! Hardwrap setlocal nowrap formatoptions+=cat
command! Syncwin windo set scrollbind! cursorbind!
command! EF exe "split $HOME/.vim/after/ftplugin/".&filetype.".vim"
command! FD filetype detect
command! -nargs=? Browser exe 'terminal ++close w3m -config '.getenv("XDG_CONFIG_HOME").'/w3m/config -bookmark '.getenv("XDG_CONFIG_HOME").'/w3m/bookmark.html '.<q-args>
command! -nargs=1 Img
      \ exe "r! find ".shellescape(expand("%:p:h"))." -maxdepth ".<args>." -type f -exec file --mime-type '{}' \\+"
      \ ."| awk -F: '$2 ~ /image\\//{print $1}'"
      \ ."| sxiv -qto - 2>/dev/null"

" Fat finger fixes/convenience abbreviations
cnoreabbrev E Explore
cnoreabbrev Colors XtermColorTable
cnoreabbrev lset setl
cnoreabbrev sudow w !sudo tee > /dev/null %
cnoreabbrev hg helpgrep
cnoreabbrev gitr system("git root")

if has('terminal')
  " ++close means close when process ends
  "   (so that I don't get a hanging term buffer)
  cnoreabbrev wt terminal ++close
  cnoreabbrev tt tab terminal ++close
  cnoreabbrev tm terminal ++close man
  cnoreabbrev tf terminal ++close lf
  cnoreabbrev tn terminal ++close joplin
  command! Reddit tab terminal ttrv
endif
" }}}
" Digraphs {{{1
let digraphs = {
      \ 'o+': 0x2295, 'O+': 0x2a01,
      \ 'e$': 0x20ac,
      \ '\|>': 0x21a6,
      \ '[[': 0x27e6, ']]': 0x27e7,
      \ 'aS': 0x1D43, 'bS': 0x1D47, 'cS': 0x1D9C, 'dS': 0x1D48, 'eS': 0x1D49, 'fS': 0x1DA0, 'gS': 0x1D4D, 'hS': 0x02B0, 'iS': 0x2071, 'jS': 0x02B2, 'kS': 0x1D4F, 'lS': 0x02E1, 'mS': 0x1D50, 'nS': 0x207F, 'oS': 0x1D52, 'pS': 0x1D56, 'rS': 0x02B3, 'sS': 0x02E2, 'tS': 0x1D57, 'uS': 0x1D58, 'vS': 0x1D5B, 'wS': 0x02B7, 'xS': 0x02E3, 'yS': 0x02B8, 'zS': 0x1DBB, 'AS': 0x1D2C, 'BS': 0x1D2D, 'DS': 0x1D30, 'ES': 0x1D31, 'GS': 0x1D33, 'HS': 0x1D34, 'IS': 0x1D35, 'JS': 0x1D36, 'KS': 0x1D37, 'LS': 0x1D38, 'MS': 0x1D39, 'NS': 0x1D3A, 'OS': 0x1D3C, 'PS': 0x1D3E, 'RS': 0x1D3F, 'TS': 0x1D40, 'US': 0x1D41, 'VS': 0x2C7D, 'WS': 0x1D42,
      \ 'as': 0x2090, 'es': 0x2091, 'is': 0x1D62, 'os': 0x2092, 'rs': 0x1D63, 'us': 0x1D64, 'vs': 0x1D65, 'xs': 0x2093, 'ys': 0x1D67,
      \ '0S': 0x2070, '1S': 0x00B9, '2S': 0x00B2, '3S': 0x00B3, '4S': 0x2074, '5S': 0x2075, '6S': 0x2076, '7S': 0x2077, '8S': 0x2078, '9S': 0x2079, '+S': 0x207A, '-S': 0x207B, '=S': 0x207C, '(S': 0x207D, ')S': 0x207E,
      \ '0s': 0x2080, '1s': 0x2081, '2s': 0x2082, '3s': 0x2083, '4s': 0x2084, '5s': 0x2085, '6s': 0x2086, '7s': 0x2087, '8s': 0x2088, '9s': 0x2089, '+s': 0x208A, '-s': 0x208B, '=s': 0x208C, '(s': 0x208D, ')s': 0x208E,
      \ '-<':  0x227a, '>-': 0x227b,
      \ 'n#': 0x2115, 'r#': 0x211d, 'z#': 0x2124 }

for [dg, code] in items(digraphs)
  exe 'digraphs ' .. dg .. ' ' .. code
endfor
" Mappings {{{1
" So I don't have to mash shift all the time
nnoremap ; :
vnoremap ; :

if has('terminal')
  tnoremap <C-w>; <C-w>:
endif

" To keep the old functionality of ;
nnoremap : ;
vnoremap : ;

" Disable q: cuz I hate it
" (also C-F does the same)
nnoremap q: <Nop>

" Normal mode shortcuts
nnoremap <leader>b :ls<CR>:b<Space>

" Retain visual mode after > and <
vnoremap < <gv
vnoremap > >gv

" visual j/k
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k
vnoremap j gj
vnoremap k gk
vnoremap gj j
vnoremap gk k

" Move visual block
xnoremap D :m '>+1<CR>gv=gv
xnoremap U :m '<-2<CR>gv=gv

" and don't break my colours (U for 'unfuck my screen please')
nnoremap <leader><c-l> :syntax sync fromstart<CR>:redraw!<CR>:diffupdate<CR><c-l>

" urlview
nnoremap <silent> U :w !urlview<CR>

" 'zoom to head level'
nnoremap zh mzzt10<c-u>`z

" Reindent the file
nnoremap <leader>= mlgg=G`lzz

" Tab mappings
nnoremap <leader>tn :tabnew<CR>
nnoremap <leader>tc :tabclose<CR>
nnoremap <leader>th :tabmove -1<CR>
nnoremap <leader>tl :tabmove +1<CR>

" Window resizing mappings
nnoremap <C-k> <C-w>+
nnoremap <C-j> <C-w>-
nnoremap <C-h> <C-W><
nnoremap <C-l> <C-W>>

" Delete hidden buffers
nmap <leader>dh <Plug>DeleteHiddenBuffers

" Switch to alernative buffer
nnoremap <leader>s<leader> :b#<CR>

" Show hidden symbols
nnoremap <leader>hs :set list!<CR>

" List marks
nnoremap <leader>mm :<C-u>marks<CR>:normal! `
nnoremap <leader>ml :<C-u>marks a-z<CR>:normal! `


" Map '0' to act as '^' on first press and '0' on second
nnoremap <expr> <silent> 0 col('.') == match(getline('.'),'\S')+1 ? '0' : '^'

" Highlight last inserted text
nnoremap gV `[v`]

" Save in insert mode
inoremap <C-Z> <C-o>:w<CR>

" Set the foldlevel
nnoremap zn :set foldlevel=
nnoremap zs :exe "set foldlevel=".foldlevel(line('.'))<CR>

" Config edit mappings
" Why :drop? It uses the file if it's already open.
" Also have to map c-c to no-op first, otherwise doesn't work
nnoremap <C-c> <nop>
nnoremap <leader><C-E><C-V> :DropToFoldedVimrc<CR>zXgg
nnoremap <leader><C-E><C-A> :DropToFoldedVimrc<CR>zXgg/^" Autocommands {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-C> :DropToFoldedVimrc<CR>zXgg/Commands {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-E> :DropToFoldedVimrc<CR>zXgg/Editor {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-G> :DropToFoldedVimrc<CR>zXgg/General {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-M> :DropToFoldedVimrc<CR>zXgg/Mappings {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-P><C-I> :DropToFoldedVimrc<CR>zXgg/Plugins {{<CR>:noh<CR>za/Installation {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-P><C-C> :DropToFoldedVimrc<CR>zXgg/Plugins {{<CR>:noh<CR>za/Config {{<CR>:noh<CR>za
nnoremap <leader><C-E><C-U> :UltiSnipsEdit<CR>

" Yank to clipboard
nnoremap <leader>d "+d
vnoremap <leader>d "+d
nnoremap <leader>D "+D
nnoremap <leader>y "+y
vnoremap <leader>y "+y
nnoremap <leader><leader>p "+p
nnoremap <leader><leader>P "+P
vnoremap <leader><leader>p "+p
vnoremap <leader><leader>P "+P
nnoremap <leader>n "_
vnoremap <leader>n "_
nnoremap <leader>yy "+yg_
nnoremap <leader>y<leader> :<c-u>let @*=@0<CR>
nnoremap Y y$

" Insert lines without moving cursor and going into insert mode
nnoremap <leader>o m`o<esc>``
nnoremap <leader>O m`O<esc>``

" Custom session maps
nnoremap <leader>ss :call sessions#SaveSession()<CR>
nnoremap <leader>sl :call sessions#LoadSession()<CR>
nnoremap <leader>sd :call sessions#DeleteSession()<CR>
nnoremap <leader>sq :call sessions#CloseSession()<CR>

" native file browsing
nnoremap <leader>f  :Lexplore<CR>

" Strip trailing whitespace (silent makes it so nothing is echoed)
nmap <silent> <leader>$ <Plug>StripTrailingWhitespace

" Buffer switching
nnoremap H :bprevious<CR>
nnoremap L :bnext<CR>

" Arg switching
nnoremap <leader>H :previous<CR>
nnoremap <leader>L :next<CR>


" Correct the last spelling error
inoremap <expr> <C-x>s &spell ? "<c-g>u<Esc>[s1z=`]a<c-g>u" : ""

" Make-ing (use Dispatch if enabled)
nnoremap <leader>m? :set makeprg<CR>
nnoremap <leader>mm :Make<CR>
nnoremap <leader>mc :Make clean<CR>

" If Dispatch not used
" :silent make<CR>\|:redraw!<CR>\|:cwindow<CR>
" :silent make clean<CR>\|:redraw!<CR>

" Vmap for searching in selection
vnoremap <leader>/ <esc>/\%V

" Allow scrolling popup menus with the mouse
if has('mouse')
  inoremap <expr> <ScrollWheelUp> pumvisible() ? "<C-p>" : "<Esc><ScrollWheelUp>"
  inoremap <expr> <ScrollWheelDown> pumvisible() ? "<C-n>" : "<Esc><ScrollWheelDown>"
endif

" 'Zoom' the current buffer in a new tab
nnoremap <C-w>Z :tab split<CR>

" Go to line:column
nnoremap <leader>gg :call input("go line:col> ")->split(":")->cursor()<CR>

" Which commit introduced this line?
nnoremap <silent><Leader>gw :call setbufvar(winbufnr(popup_atcursor(systemlist("cd " . shellescape(fnamemodify(resolve(expand('%:p')), ":h")) . " && git log --no-merges -n 1 -L " . shellescape(line("v") . "," . line(".") . ":" . resolve(expand("%:p")))), { "padding": [1,1,1,1], "pos": "botleft", "wrap": 0, "highlight": "StatusLine"})), "&filetype", "git")<CR>

" Avoid paste hijacking in insert mode
inoremap <c-r> <c-r><c-o>

" Up/down arrows on the command line search for commands starting with current
" string. I want <c-n/p> to do the same.
cnoremap <expr> <c-n> wildmenumode() ? "\<c-n>" : "\<down>"
cnoremap <expr> <c-p> wildmenumode() ? "\<c-p>" : "\<up>"

" Use <Tab> and <S-Tab> to jump to next search result,
" while keeping <Tab>'s wildmenu function.
set wildcharm=<c-z>
cnoremap <expr> <Tab>   getcmdtype() =~ '[?/]' ? "<c-g>" : "<c-z>"
cnoremap <expr> <S-Tab> getcmdtype() =~ '[?/]' ? "<c-t>" : "<S-Tab>"

" Easier pasting
nnoremap <leader>p "0p
vnoremap <leader>p "0p
nnoremap <leader>P "0P
vnoremap <leader>P "0P

" Highlight one or more lines
highlight LineHighlight ctermbg=darkgray guibg=darkgray cterm=bold
nnoremap <silent> <leader>hl :call matchadd('LineHighlight', '\%'.line('.').'l')<CR>
vnoremap <silent> <leader>hl :call matchadd('LineHighlight', '\%'.line('.').'l')<CR>
nnoremap <silent> <leader>hc :call clearmatches()<CR>
vnoremap <silent> <leader>hc :call clearmatches()<CR>

" have n always search forward
noremap <expr> <SID>(search-forward) 'Nn'[v:searchforward]
" and N always search backward
noremap <expr> <SID>(search-backward) 'nN'[v:searchforward]
" and center search results on the screen
nmap n <SID>(search-forward)zzzv
xmap n <SID>(search-forward)zzzv
nmap N <SID>(search-backward)zzzv
xmap N <SID>(search-backward)zzzv

" backspace to switch to alternate buffer
nnoremap <backspace> <c-^>
" Autocommands {{{1
if has('autocmd')
  augroup vimrc_sets
    autocmd!
    autocmd InsertEnter * setlocal nocursorline
    autocmd InsertLeave * setlocal cursorline
    autocmd ColorScheme * highlight ColorColumn ctermbg=233
  augroup END
  augroup vimrc_commands
    autocmd!
    autocmd BufWritePre * silent StripTrailingWhitespace

  augroup END
endif
" }}}
